dependencies {
  compile project(':common'),
      'org.jooq:jooq:3.6.2',
      'org.jooq:jooq-meta:3.6.2',
      'org.postgresql:postgresql:9.4-1202-jdbc42',
      'javax.mail:mail:1.4.7',
      'javax.activation:activation:1.1.1',
      'com.sendgrid:sendgrid-java:2.2.2'
}

jar {
  manifest {
    attributes 'Main-Class': 'server.Server'
  }
  exclude 'CreateTables.sql'
}

shadowJar {
  dependsOn ':common:shadowJar'
  exclude 'CreateTables.sql'
}

// ***** Generate JOOQ classes from database *****

buildscript {
  repositories {
    mavenLocal()
    mavenCentral()
  }
  dependencies {
    classpath 'org.jooq:jooq-codegen:3.6.2',
        'org.postgresql:postgresql:9.4-1202-jdbc42'
  }
}

import javax.xml.bind.JAXB
import org.jooq.util.GenerationTool
import groovy.xml.MarkupBuilder

final def String srcDir = file('src/main/java').canonicalPath
final def String jooqClassesDir = srcDir + '/server/jooq'
task generateJooqClasses << {
  description 'Generates jOOQ classes used by server from Postgres database tables.'
  if (file(jooqClassesDir).exists()) {
    println 'jOOQ classes already generated'
    return
  }
  println 'Generating jOOQ classes into ' + jooqClassesDir
  def writer = new StringWriter()
  def jooqXml = new MarkupBuilder(writer)
      .configuration('xmlns': 'http://www.jooq.org/xsd/jooq-codegen-3.6.0.xsd'
  ) {
    jdbc() {
      driver('org.postgresql.Driver')
      url('jdbc:postgresql://localhost/debtmanager')
      user('debtmanager')
      password('debtmanager')
    }
    generator() {
      database() {
        name('org.jooq.util.postgres.PostgresDatabase')
        inputSchema('debtmanager')
        includes('.*')
      }
      generate() {
      }
      target() {
        packageName('server.jooq')
        directory(srcDir)
      }
    }
  }
  GenerationTool.generate(
      JAXB.unmarshal(new StringReader(writer.toString()), org.jooq.util.jaxb.Configuration.class)
  )
}

compileJava.dependsOn generateJooqClasses

task cleanJooqClasses << {
  println 'Removing jOOQ generated classes from ' + jooqClassesDir
  delete jooqClassesDir
}

clean.dependsOn cleanJooqClasses